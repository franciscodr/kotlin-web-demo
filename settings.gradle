import groovy.json.JsonSlurper

JsonSlurper slurper = new JsonSlurper()
file('kotlin.web.demo.common/src/main/resources/compilers-config.json').toPath().withReader { reader ->
    gradle.ext.kotlinConfigs = slurper.parse(reader)
}

file('kotlin.web.demo.common/src/main/resources/arrow-versions-config.json').toPath().withReader { reader ->
    gradle.ext.arrowConfigs = slurper.parse(reader)
    gradle.ext.supportedKotlinVersions = gradle.ext.arrowConfigs.collectEntries { v ->
        [v.version, v.supportedKotlinVersions]
    }
}

def arrowLibraries = gradle.ext.arrowConfigs.collect({ ':arrow:' + it.version });
include(arrowLibraries as String[])

def kotlinWrappers = gradle.ext.kotlinConfigs.collect({ ':versions:' + it.version });
include(kotlinWrappers as String[])
include 'arrow', ':kotlin.web.demo.executors', ':kotlin.web.demo.server', ':kotlin.web.demo.common', ':kotlin.web.demo.backend:compilers', ':kotlin.web.demo.frontend', 'kotlin.web.demo.test'