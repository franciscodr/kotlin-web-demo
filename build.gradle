/*
 * Copyright 2000-2016 JetBrains s.r.o.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
    repositories {
        mavenCentral()
        maven { url "https://dl.bintray.com/kotlin/kotlin-dev" }
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
}
ext.kotlinVersion = kotlin_version
ext.kotlinConfigs = gradle.ext.kotlinConfigs
ext.kotlinVersions = kotlinConfigs.collect { it.version }
ext.arrowConfigs = gradle.ext.arrowConfigs
ext.arrowVersions = arrowConfigs.collect { it.version }

allprojects {
    repositories {
        jcenter()
        maven { url "https://dl.bintray.com/kotlin/kotlin-dev" }
        maven { url "https://kotlin.bintray.com/kotlin-eap-1.2" }
    }
}

configure(allprojects.findAll({
    it.name != 'kotlin.web.demo.frontend'
})) {
    apply plugin: 'java'
    sourceCompatibility = java_version
    targetCompatibility = java_version

    repositories {
        mavenCentral()
    }

    sourceSets {
        test {
            java {
                srcDir 'test'
            }
        }
    }

    dependencies {
        compile group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '2.9.8', transitive: true
        compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.9.8', transitive: true
        compile group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: '2.9.8', transitive: true
        compile 'commons-logging:commons-logging:1.2'
    }
}

configure([
        project(':kotlin.web.demo.server'),
        project(':kotlin.web.demo.backend'),
        project(':kotlin.web.demo.common'),
        project(':kotlin.web.demo.executors'),
        project(':kotlin.web.demo.backend:compilers'),
        project(':kotlin.web.demo.test')
]) {
    dependencies {
        compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
        compile "com.fasterxml.jackson.module:jackson-module-kotlin:2.9.8"
    }
}

configure([
        project(':kotlin.web.demo.server'),
        project(':kotlin.web.demo.backend'),
        project(':kotlin.web.demo.backend:compilers'),
        project(':kotlin.web.demo.test')
]) {
    dependencies {
        compile project(':kotlin.web.demo.common')
    }
}

task copyKotlinLibs() {}

for (arrowVersion in arrowVersions) {
    copyKotlinLibs.dependsOn(":arrow:$arrowVersion:copyKotlinLibs")
}

for (kotlinVersion in kotlinVersions) {
    copyKotlinLibs.dependsOn(":versions:$kotlinVersion:copyKotlinLibs")
}

project("kotlin.web.demo.executors") {
    dependencies {
        compile group: 'junit', 'name': 'junit', 'version': '4.12'
    }

    task copyToLib(type: Copy) {
        into "$buildDir/libs"
        from configurations.compile
    }
    jar.dependsOn(copyToLib)
}

project('kotlin.web.demo.test') {
    dependencies {
        testCompile project(':kotlin.web.demo.backend:compilers')
        testCompile project(':kotlin.web.demo.backend')
        testCompile project(':kotlin.web.demo.server')
        testCompile group: 'org.jetbrains.kotlin', 'name': 'kotlin-test', 'version': '1.1.51'
        testCompile group: 'junit', 'name': 'junit', 'version': '4.12'
    }
    for (kotlinVersion in kotlinVersions) {
        test.dependsOn(":versions:$kotlinVersion:classes")
    }
    test.dependsOn(":kotlin.web.demo.executors:jar")
//    test.dependsOn(":copyKotlinLibs")
}

project(':versions').jar { onlyIf { false } }