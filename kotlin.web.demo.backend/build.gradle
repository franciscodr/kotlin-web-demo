buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
    }
}

apply plugin: "kotlin"
apply plugin: 'war'

dependencies {
    compile project(":kotlin.web.demo.backend:compilers")
    compile project(":kotlin.web.demo.executors")
    providedCompile group: 'javax.servlet', name: 'javax.servlet-api', version: '3.0.1'
}

for (arrowVersion in arrowVersions) {
    evaluationDependsOn(":arrow:$arrowVersion")
}

for (kotlinVersion in kotlinVersions) {
    evaluationDependsOn(":versions:$kotlinVersion")
}

war {
    archiveName "WebDemoBackend.war"

    from(project(':kotlin.web.demo.executors').projectDir) {
        include 'executors.policy.template'
    }

    for (kotlinVersion in kotlinVersions) {
        def kotlinProject = project(":versions:$kotlinVersion")
        from(kotlinProject.sourceSets.main.output) {
            into("WEB-INF/kotlin-wrappers/$kotlinVersion/classes")
        }
        from(kotlinProject.configurations['kotlinJS'].collect { zipTree(it) }) {
            into "WEB-INF/kotlin-wrappers/$kotlinVersion/js"
        }
        from(kotlinProject.configurations['kotlinJVM']) {
            into "WEB-INF/kotlin-wrappers/$kotlinVersion/kotlin"
        }
        from(kotlinProject.configurations['library']) {
            into "WEB-INF/kotlin-wrappers/$kotlinVersion/libraries"
        }
    }

    for (arrowVersion in arrowVersions) {
        def arrowProject = project(":arrow:$arrowVersion")
        from(arrowProject.configurations['library']) {
            into "WEB-INF/kotlin-wrappers/arrow/$arrowVersion/libraries"
        }
    }
}